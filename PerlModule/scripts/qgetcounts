#!/usr/bin/perl -w
use strict;
use QUANDICO qw<:DEFAULT :FLAGS>;
use version 0.77;

=head1 NAME

QUANDICO - Package with helper scripts for the R application 'quandico'

=head1 VERSION

Version 1.12

=cut

=head1 SYNOPSIS

The is a script to extract amplicon read counts from mapped reads (SAM/BAM). 

	# help is available:
   qgetcounts --help
   
   # usage with demo data from <http://github.com/reineckef/quandico/Data>
   qgetcounts -i M62_NA13019.bam -a CNA902Y.bed
   qgetcounts -i M62_NA12878.bam -a CNA902Y.bed
   

=cut

# Getopt::Long::Descriptive - input parameters
my ( $opt, $usage ) = describe_options(
	'qgetcounts %o <options>',
	['
  input file and global options:'
	],
	['mapfile|i=s',   'input (BAM or SAM) with mapped reads'],
	['amplicons|a=s', 'amplicon coordinates (BED format)'],
	['
  output files:'
	],
	['output|o=s', 'name for output file'],
	['
  detailed settings:'
	],
	['properly!',   'require reads to be properly paired [off]',                  {default => 0}],
	['minmapq=i',   'minimal mapping quality to count (0=off) [5]',               {default => 5}],
	['minmapped=f', 'minimal fraction (0-1) or bases (>1) mapped per read [0.5]', {default => 0.5}],
	['primerlen=i', 'avg length of primers already clipped [25]',                 {default => 25}],
	['tolerance=i', 'number of bases to tolerate around ends [10]',               {default => 10}],
	['primary!',    'only use primary mappings [on]',                             {default => 1}],
	['
  external tools:'
	],
	['samtools=s', 'path to samtools executable [samtools]', {default => 'samtools'}],
	['
  standard options:'
	],
	['verbose|v+', 'control verbosity, multiple levels [1]', {default => 1}],
	['version|V', "show the version ($QUANDICO::VERSION) and exit"],
	['dump|D',     "dump all options and exit"],
	['quiet|q',   'suppress all output except errors [off]', {implies => {verbose => 0}}],
	['help|h',    'show this help screen'],
	['

# usage with demo data (available from <http://github.com/reineckef/quandico/Data>)
qgetcounts -i M62_NA13019.bam -a CNA902Y.bed
qgetcounts -i M62_NA12878.bam -a CNA902Y.bed

'
]
);
print( $usage->text, 1 ), exit if $opt->help;
if ( $opt->version ) {
	my $name = $0;
	$name =~ s!^\.\/!!;
	printf "This is %s %s\nUse flags -h or --help for help.\n", $name, $QUANDICO::VERSION->stringify;
	exit;
}
if ( not $opt->mapfile or not $opt->amplicons ) {
	print STDERR "Options --mapfile BAMFILE and --amplicons BEDFILE are mandatory!\n\n";
	print( $usage->text, 0 );
	exit;
}

# choose basename to match input, without extension
if ( not $opt->{output} ) {
	$opt->{output} = $opt->{mapfile};
	$opt->{output} =~ s/[sb]am$/tsv/gsim;
}
if ( $opt->{dump} ) {
	print Dumper $opt;
	exit;
}

# convert sam to bam
if ( $opt->{mapfile} =~ /\.sam$/i ) {
	print "# Creating the BAM file because input $opt->{mapfile} is in SAM format...\n";
	my $bam = $opt->{mapfile};
	$bam =~ s/sam$/bam/i;
	my $make_index = sprintf qq~%s view -L %s -uS %s | %s sort - > %s~,    #
	$opt->{samtools},                                                      # samtools
	$opt->{amplicons},                                                     # BED
	$opt->{mapfile},                                                       # SAM
	$opt->{samtools},                                                      # samtools
	$bam;                                                                  # final output
	my $made = `$make_index`;
	print $made, "\n";
	$opt->{mapfile} = $bam;
} ## end if ( $opt->{mapfile} =~ /\.sam$/i)

# check for existance of an index
my $index = $opt->{mapfile} . '.bai';
if ( not -e $index ) {
	print "# Creating the BAM index as the file '$index' is not there...\n";
	my $make_index = sprintf qq~%s index %s~, $opt->{samtools}, $opt->{mapfile};
	my $made = `$make_index`;
	print $made, "\n";
}

# preset common options for samtools calls
my $properly = $opt->{properly} ? '-F 8 -f 2 ' : '';
$opt->{cmd} = sprintf qq~%s view %s%s~, $opt->{samtools}, $properly, $opt->{mapfile};
$opt->{po} = $opt->{primerlen} + $opt->{tolerance};

# open results file for writing
open my $writer, '>', $opt->{output};

# open bed file
open my $bed, '<', $opt->{amplicons};

# parse all bed regions (lines)
while ( my $region = <$bed> ) {
	print "# region: ", $region if $opt->{verbose} > 1;
	chomp($region);
	my @BED = split( /\t/, $region );
	my ( $left, $right ) = ( $BED[1] - $opt->{primerlen}, $BED[2] + $opt->{primerlen} );
	my @Counts = get_counts( $opt->{cmd} . ( sprintf " %s:%d-%d", $BED[0], $left, $right ), $left, $right );
	my $name = $BED[3] || 'NONAME';
	my $gene = $BED[5] || 'NOGENE';
	print $writer    # save to file
	join( "\t", $BED[0], $BED[1], 0, 'N', $name, $Counts[0], $gene ), "\n",    # left count
	join( "\t", $BED[0], $BED[2], 1, 'N', $name, $Counts[1], $gene ), "\n";    # right count

	if ( $opt->{verbose} ) {
		print                                                                  # print to console
		join( "\t", $BED[0], $BED[1], 0, 'N', $name, $Counts[0], $gene ), "\n",    # left count
		join( "\t", $BED[0], $BED[2], 1, 'N', $name, $Counts[1], $gene ), "\n";    # right count
	}
} ## end while ( my $region = <$bed> )
close $writer;
exit;

# get_counts
sub get_counts {
	my $cmd   = shift;                                                             # command to run (samtools)
	my $left  = shift;
	my $right = shift;
	my @Min   = ( [$left, $left + $opt->{po}], [$right - $opt->{po}, $right] );
	my @Count = ( 0, 0 );
	if ( $opt->{verbose} > 1 ) {
		print $cmd, "\n";
	}
	open my $reader, '-|', $cmd;
	READ: while ( my $line = <$reader> ) {
		my @SAM = split( /\t/, $line );
		if ( $opt->{primary} ) {
			next READ if $SAM[4] & SUPPLEMENT;
		}
		next READ unless $SAM[4] >= $opt->{minmapq};
		if ( $opt->{minmapped} > 0 ) {
			my $mapped = 0;
			while ( $SAM[5] =~ /(\d+)M/gsm ) {
				$mapped += $1;
			}
			if ( $opt->{minmapped} > 0 ) {
				if ( $opt->{minmapped} <= 1 ) {
					next READ if $mapped / length( $SAM[9] ) < $opt->{minmapped};
				}
				else {
					next READ if $mapped < $opt->{minmapped};
				}
			} ## end if ( $opt->{minmapped} > 0 )
		} ## end if ( $opt->{minmapped} > 0 )
		my $dir = 0;
		if ( $SAM[1] & REVERSE ) {
			$dir = 1;
			$SAM[5] =~ s/\d+S$//;
			while ( $SAM[5] =~ /(\d+)[MDS]/gsm ) {
				$SAM[3] += $1;
			}
		}
		next READ if $SAM[3] < $Min[$dir]->[0];
		next READ if $SAM[3] > $Min[$dir]->[1];
		$Count[$dir]++;
	} ## end READ: while ( my $line = <$reader> )
	return @Count;
} ## end sub get_counts

=head1 AUTHOR

Frank Reinecke, C<< <frank.reinecke at qiagen.com> >>

=head1 BUGS

Please report any bugs or feature requests to C<frank.reinecke at qiagen.com>, 
or through the website at L<https://github.com/reineckef/quandico>. 


=head1 SUPPORT

You can find documentation for this module with the perldoc command.

    perldoc QUANDICO


You can also look for information at:

=over 4

=item * GitHub (report bugs there)

L<https://github.com/reineckef/quandico>


=back


=head1 ACKNOWLEDGEMENTS


=head1 LICENSE AND COPYRIGHT

Copyright 2014 Frank Reinecke.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; version 3 dated June, 2007 or at your option
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

A copy of the GNU General Public License is available in the source tree;
if not, write to the Free Software Foundation, Inc.,
59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.


=cut
